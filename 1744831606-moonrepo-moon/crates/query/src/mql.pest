WHITESPACE = _{ " " }

key = @{ ASCII_ALPHANUMERIC+ }

// Other characters are required for IDs
value = @{ (ASCII_ALPHANUMERIC | "-" | "_" | "/" | "." | "@")+ }

value_glob = @{ (ASCII_ALPHANUMERIC | "-" | "_" | "*" | "!" | "." | "," | "/" | "\\" | "{" | "}" | "<" | ">" | "[" | "]" | "?" | "$" | ":" | "@")+ }

value_list = { "[" ~ value ~ ("," ~ value)* ~ "]" }

// Operators

and   = { "&&" | "AND" }
or    = { "||" | "OR" }
eq    = { "=" }
neq   = { "!=" }
like  = { "~" }
nlike = { "!~" }

logic_op = _{ and | or }
cmp_op   = _{ eq | neq }
like_op  = _{ like | nlike }

// Expressions

comparison_literal  = _{ key ~ cmp_op ~ (value_list | value) }
comparison_wildcard = _{ key ~ like_op ~ value_glob }
comparison          =  { comparison_wildcard | comparison_literal }

expr       = _{ comparison ~ (logic_op ~ (comparison | expr_group))* }
expr_group =  { "(" ~ expr ~ ")" }

query = _{
    SOI ~ expr ~ EOI
}
